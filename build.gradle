plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'de.kontext_e.jqassistant.gradle' version '2.1.0'
}

group = 'de.kontext-e.jqassistant.plugin'
version = '0.1.0-SNAPSHOT'
project.ext["jqaversion"] = "2.1.0"

repositories {
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    implementation 'com.buschmais.jqassistant.core:scanner:' + project.jqaversion
    implementation 'com.buschmais.jqassistant.core:report:' + project.jqaversion
    implementation 'com.buschmais.jqassistant.plugin:common:' + project.jqaversion
    implementation 'com.buschmais.jqassistant.core:store:' + project.jqaversion
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'com.sun.xml.bind:jaxb-impl:4.0.2'
    implementation 'org.slf4j:slf4j-simple:2.0.13'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation 'com.buschmais.jqassistant.core:test:' + project.jqaversion
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'ke-jqassistant-plugins'
                packaging = 'jar'
                description = 'jQAssistant Git plugin sponsored by Kontext E'
                url = 'https://github.com/kontext-e/jqassistant-git-plugin'

                licenses {
                    license {
                        name = 'GNU General Public License, v3'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.html'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer{
                        id = 'william-thimm'
                        name = 'William Thimm'
                        email = 'w.thimm@kontext-e.de'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.version.toUpperCase().contains("SNAPSHOT")){
                url = "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials{
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
        mavenLocal()
    }
}

signing {
    //I assume this only works with gpg installed.
    //On windows this comes packaged with Kleopatra
    useGpgCmd()
    sign publishing.publications.mavenJava
}